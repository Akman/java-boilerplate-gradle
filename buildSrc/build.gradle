/*
  Gradle Build Boilerplate Project

  https://github.com/akman/java-boilerplate-gradle/buildSrc

  The MIT License (MIT)

  Copyright (C) 2019 - 2020 Alexander Kapitman <akman.ru@gmail.com>

  Permission is hereby granted, free of charge, to any person obtaining
  a copy of this software and associated documentation files (the "Software"),
  to deal in the Software without restriction, including without limitation
  the rights to use, copy, modify, merge, publish, distribute, sublicense,
  and/or sell copies of the Software, and to permit persons to whom
  the Software is furnished to do so, subject to the following conditions:

  The above copyright notice and this permission notice shall be included
  in all copies or substantial portions of the Software.

  THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
  IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
  FITNESS FOR A PARTICULAR PURPOSE AND NONINFINGEMENT. IN NO EVENT SHALL
  THE AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
  LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING
  FROM, OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER
  DEALINGS IN THE SOFTWARE.
*/

plugins {
  id 'groovy'

  // https://github.com/CodeNarc/CodeNarc
  id 'codenarc'

  // https://github.com/hierynomus/license-gradle-plugin
  id 'com.github.hierynomus.license' version '0.15.0'

  // https://github.com/ben-manes/gradle-versions-plugin
  id 'com.github.ben-manes.versions' version '0.27.0'
  
  id 'java-gradle-plugin'
  
  // https://plugins.gradle.org/docs/publish-plugin
  // https://plugins.gradle.org/docs/submit
  id 'com.gradle.plugin-publish' version '0.10.1'
}

gradlePlugin {
  plugins {
    vscodePlugin {
      id = 'ru.akman.vscode'
      implementationClass = 'ru.akman.plugins.VSCodePlugin'
    }
  }
}

pluginBundle {
  website = 'https://github.com/akman/java-boilerplate-gradle/buildSrc'
  vcsUrl = 'https://github.com/akman/java-boilerplate-gradle/buildSrc'
  plugins {
    vscodePlugin {
      displayName = 'Gradle VSCode plugin'
      description = 'Generates all VS Code files'
      tags = ['vscode']
      version = '0.1'
    }
  }
}

repositories {
  jcenter()
}

ext {
  defaultEncoding = 'UTF-8'
}

java {
  sourceCompatibility = JavaVersion.VERSION_13
  targetCompatibility = JavaVersion.VERSION_13
}

dependencies {

  implementation(
    localGroovy(),
    gradleApi()
  )

  // The Gradle TestKit is a library that aids in testing Gradle plugins and
  // build logic generally. At this time, it is focused on functional testing.
  // That is, testing build logic by exercising it as part of
  // a programmatically executed build.
  // Over time, the TestKit will likely expand to facilitate other kinds of
  // tests.
  // https://docs.gradle.org/current/userguide/test_kit.html

  // testImplementation(
  //   gradleTestKit(),
  // )

  // Spock

  testImplementation(
    'org.spockframework:spock-core:1.3-groovy-2.5',
    // only necessary if Hamcrest matchers are used
    // 'org.hamcrest:hamcrest-core:1.3',
  )

  // testRuntimeOnly(
  //   // allows mocking of classes, in addition to interfaces
  //   'net.bytebuddy:byte-buddy:1.9.3',
  //   // allows mocking of classes without default constructor,
  //   // together with CGLIB
  //   'org.objenesis:objenesis:2.6',
  // )

  // JUnit 5

  // testImplementation(
  //   'org.junit.jupiter:junit-jupiter-api:5.4.2',
  //   'org.junit.jupiter:junit-jupiter-params:5.4.2',
  //   'org.mockito:mockito-junit-jupiter:2.28.2',
  // )

  // testRuntimeOnly(
  //   'org.junit.jupiter:junit-jupiter-engine:5.4.2',
  // )
}

[compileGroovy, compileTestGroovy]*.options*.encoding = defaultEncoding

license {
  // Specify location of header to use in comparisons,
  // default to project.file('LICENSE')
  header file("${projectDir}/config/license/LICENSE")
  // Skip over files that have some header already, which might not be
  // the one specified in the header parameter, defaults to false
  skipExistingHeaders false
  // Prevent tasks from stopping the build, defaults to false
  ignoreFailures true
  // Be extra strict in the formatting of existing headers, defaults to false
  strictCheck true
  headerDefinitions {
    javadoc_style {
      firstLine = '/*'
      beforeEachLine = '  '
      endLine = '*/'
      afterEachLine = ''
      // skipLinePattern = ''
      firstLineDetectionPattern = '(\\s|\\t)*/\\*.*$'
      lastLineDetectionPattern = '.*\\*/(\\s|\\t)*$'
      allowBlankLines = true
      isMultiline = true
      padLines = false
    }
    xml_style {
      firstLine = '<!--'
      beforeEachLine = '  '
      endLine = '-->'
      afterEachLine = ''
      skipLinePattern = '^<\\?xml.*>$'
      firstLineDetectionPattern = '(\\s|\\t)*<!--.*$'
      lastLineDetectionPattern = '.*-->(\\s|\\t)*$'
      allowBlankLines = true
      isMultiline = true
      padLines = false
    }
  }
  mapping([
    'groovy': 'JAVADOC_STYLE',
  ])
  includes([
    '**/*.groovy',
  ])
  excludes([
    '**/log4j*.xml',
    '**/*.properties',
    '**/*.png',
  ])
  ext {
    projectName = 'Gradle Build Boilerplate Project'
    projectUrl = 'https://github.com/akman/java-boilerplate-gradle/buildSrc'
    inceptionYear = 2019
    currentYear = Calendar.getInstance().get(Calendar.YEAR)
    copyrightYear = "${inceptionYear}"
    if (inceptionYear != currentYear) {
      copyrightYear += " - ${currentYear}"
    }
    copyrightName = 'Alexander Kapitman'
    copyrightEmail = 'akman.ru@gmail.com'
  }
}

codenarc {
  // The version of the code quality tool to be used.
  toolVersion = '1.5'

  // The CodeNarc configuration file to use.
  // https://github.com/CodeNarc/CodeNarc/tree/master/src/main/resources/rulesets
  configFile = file("${projectDir}/config/codenarc/codenarc.xml")

  // Whether to allow the build to continue if there are warnings.
  ignoreFailures = true

  // The maximum number of priority 1 violations allowed before failing the build.
  maxPriority1Violations = 0

  // The maximum number of priority 2 violations allowed before failing the build.
  maxPriority2Violations = 0

  // The maximum number of priority 3 violations allowed before failing the build.
  maxPriority3Violations = 0

  // The directory where reports will be generated.
  reportsDir = file("${project.reporting.baseDir}/codenarc")

  // The format type of the CodeNarc report. One of html, xml, text, console.
  reportFormat = 'html'
}

test {
  testLogging {
    showStandardStreams = true
    showStackTraces = true
    displayGranularity = -1
    exceptionFormat = 'short'
    events(
      //'started',
      'passed',
      'skipped',
      'failed',
    )
    debug {
      displayGranularity = 0
      exceptionFormat = 'full'
      events(
        'started',
        'passed',
        'skipped',
        'failed',
      )
    }
  }
}

defaultTasks 'clean', 'build'
